buildscript {
    repositories {
        maven { url = 'https://maven.minecraftforge.net' }
        maven { url = 'https://maven.parchmentmc.org' }
        maven {
            name = "Mixin"
            url = 'https://repo.spongepowered.org/repository/maven-public/'
        }
        mavenCentral()
    }
    dependencies {
        classpath(group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true) {
            exclude group: 'org.apache.logging.log4j'
        }
        classpath 'org.apache.logging.log4j:log4j-api:2.11.2'
        classpath 'org.apache.logging.log4j:log4j-core:2.11.2'
        classpath 'org.parchmentmc:librarian:1.+'
        classpath group: "org.spongepowered", name: "mixingradle", version: "0.7-SNAPSHOT"
    }
}

plugins {
    id 'eclipse'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.parchmentmc.librarian.forgegradle'
apply plugin: 'org.spongepowered.mixin'

version = '0.1.0'
group = "xyz.brassgoggledcoders.${modId}"
archivesBaseName = "${modId}"

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

println "Java: ${System.getProperty 'java.version'}, JVM: ${System.getProperty 'java.vm.version'} (${System.getProperty 'java.vendor'}), Arch: ${System.getProperty 'os.arch'}"

sourceSets {
    main {
        resources {
            srcDir 'src/generated/resources'
        }
    }
}

minecraft {
    mappings channel: 'parchment', version: '2022.10.16-1.19.2'

    runs {
        client {
            workingDirectory project.file('run/client')

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            property 'forge.enabledGameTestNamespaces', "${modId}"
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"

            args "-mixin.config=${modId}.mixins.json"


            mods {
                iberia_rediscovered {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run/server')

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            property 'forge.enabledGameTestNamespaces', "${modId}"
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"

            args "-mixin.config=${modId}.mixins.json"

            mods {
                iberia_rediscovered {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run/data')

            property 'forge.logging.markers', 'REGISTRIES'
            property 'forge.logging.console.level', 'debug'
            property 'mixin.env.remapRefMap', 'true'
            property 'mixin.env.refMapRemappingFile', "${projectDir}/build/createSrgToMcp/output.srg"

            args '--mod', "${modId}", '--all', '--output', file('src/generated/resources/'),
                    '--existing', file('src/main/resources/')

            args "-mixin.config=${modId}.mixins.json"

            mods {
                iberia_rediscovered {
                    source sourceSets.main
                }
            }
        }
    }
}

configurations {
    shade
}

shadowJar {
    configurations = [project.configurations.shade]
    relocate 'com.tterrag.registrate', "xyz.brassgoggledcoders.${modId}.repack.registrate"
    classifier ''
}

repositories {
    mavenLocal()
    maven {
        name "Registrate"
        url "https://maven.tterrag.com/"
        content {
            includeGroup "com.tterrag.registrate"
        }
    }
    maven {
        name "The One Probe"
        url "https://cfa2.cursemaven.com"
        content {
            includeGroup "curse.maven"
        }
    }
    maven {
        name = "JEI"
        url = "https://dvs1.progwml6.com/files/maven"
        content {
            includeGroup "mezz.jei"
        }
    }
}

dependencies {
    minecraft 'net.minecraftforge:forge:1.19.2-43.1.47'

    def registrate = "com.tterrag.registrate:Registrate:MC1.19-1.1.5"
    implementation fg.deobf(registrate)
    shade registrate

    runtimeOnly fg.deobf("curse.maven:the_one_probe-245211:3965693")

    compileOnly fg.deobf("mezz.jei:jei-1.19.2-forge-api:11.4.0.282")
    compileOnly fg.deobf("mezz.jei:jei-1.19.2-common-api:11.4.0.282")
    runtimeOnly fg.deobf("mezz.jei:jei-1.19.2-forge:11.4.0.282")
}

jar {
    classifier("nodep")
    manifest {
        attributes([
                "Specification-Title"     : "${modId}",
                "Specification-Vendor"    : "Brass Goggled Coders",
                "Specification-Version"   : "1",
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : project.jar.archiveVersion,
                "Implementation-Vendor"   : "Brass Goggled Coders",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                "MixinConfigs"            : "${modId}.mixins.json"
        ])
    }
}

mixin {
    add sourceSets.main, "${modId}.refmap.json"
}

reobf {
    shadowJar {}
}

jar.finalizedBy('reobfJar')

build.dependsOn shadowJar
build.dependsOn reobfShadowJar

artifacts {
    archives shadowJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
        }
    }
    repositories {
        maven {
            url "file://${project.projectDir}/mcmodsrepo"
        }
    }
}

gradle.projectsEvaluated {
    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xmaxerrs" << "10000"
    }
}