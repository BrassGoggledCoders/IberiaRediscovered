buildscript {
    repositories {
        mavenCentral()
        jcenter()
        maven {
            name = "forge"
            url = "http://maven.minecraftforge.net"
        }
        maven {
            name = "mixin"
            url = 'https://repo.spongepowered.org/repository/maven-public/'
        }
    }
    dependencies {
        classpath group: "net.minecraftforge.gradle", name: "ForgeGradle", version: "4.1.+", changing: true
        classpath group: "org.spongepowered", name: "mixingradle", version: "0.7-SNAPSHOT"
    }
}

plugins {
    id 'com.github.johnrengelman.shadow' version '5.2.0'
}

apply plugin: "net.minecraftforge.gradle"
apply plugin: 'org.spongepowered.mixin'
apply plugin: "maven"
apply plugin: "java"
apply plugin: "idea"

version = "${minecraft_version}-" + getVersion()
group = "${group}"
archivesBaseName = "${mod_name}"

sourceSets {
    main {
        resources {
            include '**/**'
            exclude '**/*.bbmodel'
            srcDirs += "$rootDir/src/generated/resources"
            exclude '.cache'
        }
    }
}

def version_properties = [
        "version"       : version,
        "mc_version"    : minecraft_version,
        "forge_version" : min_forge_version,
        "loader_version": loader_version
]

processResources {
    outputs.upToDateWhen { false }
    from(sourceSets.main.resources) {
        include "META-INF/mods.toml"
        expand version_properties
    }
    into "$buildDir/resources/main/"
}

minecraft {
    mappings channel: "snapshot", version: "${mappings_version}"

    runs {
        client {
            workingDirectory project.file("run/client")

            properties 'mixin.env.disableRefMap': 'true'

            arg "-mixin.config=iberia_rediscovered.mixins.json"

            mods {
                iberia_rediscovered {
                    source(sourceSets.main)
                }
            }
        }

        server {
            workingDirectory project.file("run/server")

            properties 'mixin.env.disableRefMap': 'true'

            arg "-mixin.config=iberia_rediscovered.mixins.json"

            mods {
                iberia_rediscovered {
                    source(sourceSets.main)
                }
            }
        }

        data {
            workingDirectory project.file("run/data")

            environment 'target', 'fmluserdevdata'

            properties 'fml.earlyprogresswindow': 'false'
            properties 'mixin.env.disableRefMap': 'true'

            arg "-mixin.config=iberia_rediscovered.mixins.json"

            args '--all', '--output', file('src/generated/resources/'),
                    '--reports',
                    '--mod', 'iberia_rediscovered', '--existing', sourceSets.main.resources.srcDirs[0]

            mods {
                iberia_rediscovered {
                    source(sourceSets.main)
                }
            }
        }
    }
}

configurations {
    shade
}

shadowJar {
    configurations = [project.configurations.shade]
    relocate 'com.tterrag.registrate', 'xyz.brassgoggledcoders.iberiarediscovered.repack.registrate'
    classifier ''
}

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        name = "forge"
        url = "https://maven.minecraftforge.net"
    }
    maven {
        name = "Registrate"
        url = "https://maven.tterrag.com/"
        content {
            includeGroup "com.tterrag.registrate"
        }
    }
    maven {
        name = "mixin"
        url = 'https://repo.spongepowered.org/repository/maven-public/'
    }
}

dependencies {
    minecraft "net.minecraftforge:forge:${minecraft_version}-${forge_version}"

    def registrate = "com.tterrag.registrate:Registrate:MC1.16.2-${registrate_version}"
    implementation fg.deobf(registrate)
    shade registrate

    annotationProcessor 'org.spongepowered:mixin:0.8.2:processor'
}

task srcJar(type: Jar) {
    from sourceSets.main.allSource
    classifier = "sources"
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = "javadoc"
    from javadoc.destinationDir
}

jar {
    classifier = "nodep"
    manifest {
        attributes([
                "Specification-Title"     : "${title}",
                "Specification-Vendor"    : "${team}",
                "Specification-Version"   : "1",
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : "${version}",
                "Implementation-Vendor"   : "${team}",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                "MixinConfigs"            : "iberia_rediscovered.mixins.json"
        ])
    }
}

reobf {
    shadowJar {}
}

build.dependsOn shadowJar
build.dependsOn reobfShadowJar

artifacts {
    archives javadocJar
    archives srcJar
}

uploadArchives {
    repositories {
        mavenDeployer {
            repository(url: "file://" + (System.getenv("local_maven") != null ? System.getenv("local_maven") : System.getenv("bamboo_local_maven")))
        }
    }
}

mixin {
    defaultObfuscationEnv "searge"
    add sourceSets.main, "iberia_rediscovered.refmap.json"
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

String getVersion() {
    String version = "${mod_version}"
    String branch = project.hasProperty("branch") ? project.branch : ""

    if (branch != "") {
        version += "-" + branch

        String buildNumber = getBuildNumber()
        if (buildNumber != "") {
            version += "." + buildNumber
        }
    }

    return version
}

static String getBuildNumber() {
    String buildNumber = System.getenv("BUILD_NUMBER") != null ? System.getenv("BUILD_NUMBER") : ""
    if (buildNumber == "") {
        buildNumber = System.getenv("bamboo_buildNumber") != null ? System.getenv("bamboo_buildNumber") : ""
    }
    return buildNumber
}

gradle.projectsEvaluated {
    tasks.withType(JavaCompile) {
        options.compilerArgs << "-Xmaxerrs" << "10000"
    }
}
